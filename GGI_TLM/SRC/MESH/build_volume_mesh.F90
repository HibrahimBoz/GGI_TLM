!
!    GGI_TLM Time domain electromagnetic field solver based on the TLM method
!    Copyright (C) 2013  Chris Smartt
!
!    This program is free software: you can redistribute it and/or modify
!    it under the terms of the GNU General Public License as published by
!    the Free Software Foundation, either version 3 of the License, or
!    (at your option) any later version.
!
!    This program is distributed in the hope that it will be useful,
!    but WITHOUT ANY WARRANTY; without even the implied warranty of
!    MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
!    GNU General Public License for more details.
!
!    You should have received a copy of the GNU General Public License
!    along with this program.  If not, see <http://www.gnu.org/licenses/>.   
!
!SUBROUTINE build_volume_mesh
!
! NAME
!     SUBROUTINE build_volume_mesh
!
! DESCRIPTION
!     build_volume_mesh:
!
!     
! COMMENTS
!     
!
! HISTORY
!
!     started 30/08/2012 CJS
!     1/11/2018 CJS:  Allow volumes to be created by filling surfaces
!
!
SUBROUTINE build_volume_mesh()

USE TLM_general
USE geometry_types
USE geometry
USE file_information
USE cell_parameters
USE constants

IMPLICIT NONE

! local variables

integer	:: volume_number

integer	:: number_of_tets
integer	:: tet_number
integer	:: cell_count

integer,allocatable	:: volume_mesh(:,:,:)

integer,allocatable	:: surface_mesh(:,:,:,:)

integer			:: ix,iy,iz
integer			:: ix1,iy1,iz1
integer			:: ix2,iy2,iz2

integer                 :: surface
integer                 :: n_faces,face_count,face
logical                 :: inside_surface

! START

  CALL write_line('CALLED: build_volume_mesh',0,output_to_screen_flag)
  
  ALLOCATE( volume_mesh(1:nx,1:ny,1:nz) )

  do volume_number=1,n_volumes

! reset volume mesh
    volume_mesh(1:nx,1:ny,1:nz)=0
   
    if (problem_volumes(volume_number)%volume_type.NE.volume_type_fill_surface) then

! The volume mesh is build from a tetrahedral volume mesh   
      number_of_tets=problem_volumes(volume_number)%number_of_tets

      do tet_number=1,number_of_tets    
    
        if (new_mesh_generation) then
      
          CALL mesh_tet_new(volume_mesh,problem_volumes(volume_number)%tet_list(tet_number))
      
        else
      
          CALL mesh_tet(volume_mesh,problem_volumes(volume_number)%tet_list(tet_number))
       
        end if
     
      end do ! next tet
      
    else

! The volume mesh is generated by 'filling' an already existing surface mesh

! Get the surface number to fill
      surface=NINT(problem_volumes(volume_number)%volume_parameters(1)) 
      
      write(*,*)'Fill surface number',surface
      
! Check that the surface exists... 
      if ( (surface.LT.1).OR.(surface.GT.n_surfaces) ) then   ! Error
        GOTO 9000
      end if

! Put the surface into a mesh structure      
      ALLOCATE( surface_mesh(1:nx,1:ny,1:nz,1:6) )
      surface_mesh(1:nx,1:ny,1:nz,1:6)=0
      
      n_faces=problem_surfaces(surface)%number_of_faces     
      
      write(*,*)'Number of surface faces=',n_faces
      
      do face_count=1,n_faces
      
        ix=problem_surfaces(surface)%face_list(face_count)%cell%i
	iy=problem_surfaces(surface)%face_list(face_count)%cell%j
	iz=problem_surfaces(surface)%face_list(face_count)%cell%k
	face=problem_surfaces(surface)%face_list(face_count)%point
                
        surface_mesh(ix,iy,iz,face)=1   ! flag the surface in the mesh structure
        
      end do ! next face
      
! loop over the whole mesh setting the cells inside a surface to 1
! This is done by projecting along x, y and z and only if all 3 projections decide a cell is inside is it set to a volume cell
! this should get around the problem of open surfaces

      ix1=max(problem_surfaces(surface)%mesh_cell_xmin-1,1)
      ix2=min(problem_surfaces(surface)%mesh_cell_xmax+1,nx)
      iy1=max(problem_surfaces(surface)%mesh_cell_ymin-1,1)
      iy2=min(problem_surfaces(surface)%mesh_cell_ymax+1,ny)
      iz1=max(problem_surfaces(surface)%mesh_cell_zmin-1,1)
      iz2=min(problem_surfaces(surface)%mesh_cell_zmax+1,nz)

      ix1=1
      ix2=nx
      iy1=1
      iy2=ny
      iz1=1
      iz2=nz

! projection in x      
      do iz=iz1,iz2
        do iy=iy1,iy2
          inside_surface=.FALSE.
      	  do ix=ix1,ix2
        
! check xmin surface and reverse inside_surface flag if we have crossed the surface to enter this cell
            if (surface_mesh(ix,iy,iz,face_xmin).NE.0) then
              inside_surface=.NOT.inside_surface
            end if

! If we are inside the surface then flag this as a volume cell            
            if (inside_surface) then
              volume_mesh(ix,iy,iz)=1
            end if
        
! check xmax surface and reverse inside_surface flag if we have crossed the surface to exit this cell
            if (surface_mesh(ix,iy,iz,face_xmax).NE.0) then
              inside_surface=.NOT.inside_surface
            end if      
               
          end do ! ix
        end do ! iy
      end do ! iz

! projection in y      
      do iz=iz1,iz2
      	do ix=ix1,ix2
          inside_surface=.FALSE.
          do iy=iy1,iy2
        
! check xmin surface and reverse inside_surface flag if we have crossed the surface to enter this cell
            if (surface_mesh(ix,iy,iz,face_ymin).NE.0) then
              inside_surface=.NOT.inside_surface
            end if

! If we are inside the surface then flag this as a volume cell            
            if ( (inside_surface).AND.(volume_mesh(ix,iy,iz).EQ.1) ) then
              volume_mesh(ix,iy,iz)=1
            else
              volume_mesh(ix,iy,iz)=0
            end if
        
! check xmax surface and reverse inside_surface flag if we have crossed the surface to exit this cell
            if (surface_mesh(ix,iy,iz,face_ymax).NE.0) then
              inside_surface=.NOT.inside_surface
            end if      
               
          end do ! iy
        end do ! ix
      end do ! iz

! projection in z      
      do iy=iy1,iy2
        do ix=ix1,ix2
          inside_surface=.FALSE.
          do iz=iz1,iz2
        
! check xmin surface and reverse inside_surface flag if we have crossed the surface to enter this cell
            if (surface_mesh(ix,iy,iz,face_zmin).NE.0) then
              inside_surface=.NOT.inside_surface
            end if

! If we are inside the surface then flag this as a volume cell            
            if ( (inside_surface).AND.(volume_mesh(ix,iy,iz).EQ.1) ) then
              volume_mesh(ix,iy,iz)=1
            else
              volume_mesh(ix,iy,iz)=0
            end if
        
! check xmax surface and reverse inside_surface flag if we have crossed the surface to exit this cell
            if (surface_mesh(ix,iy,iz,face_zmax).NE.0) then
              inside_surface=.NOT.inside_surface
            end if      
               
          end do ! iz
        end do ! ix
      end do ! iy

! Deallocate the mesh structure
      DEALLOCATE( surface_mesh )
            
    end if   ! volume_type.EQ.fill_surface 
    
! count cells
    cell_count=0

    do iz=1,nz
      do iy=1,ny
        do ix=1,nx
      
          if (volume_mesh(ix,iy,iz).NE.0) cell_count=cell_count+1
        
        end do ! ix
      end do ! iy
    end do ! iz
    
! Allocate memory for the mesh

    problem_volumes(volume_number)%number_of_cells=cell_count
    
    if (cell_count.gt.0) then
    
      ALLOCATE( problem_volumes(volume_number)%cell_list(1:cell_count) )

! loop over mesh allocating the cell list    
      cell_count=0
      do iz=1,nz
        do iy=1,ny
          do ix=1,nx
        
            if (volume_mesh(ix,iy,iz).NE.0) then
          
              cell_count=cell_count+1
              problem_volumes(volume_number)%cell_list(cell_count)%cell%i=ix
              problem_volumes(volume_number)%cell_list(cell_count)%cell%j=iy
              problem_volumes(volume_number)%cell_list(cell_count)%cell%k=iz
              problem_volumes(volume_number)%cell_list(cell_count)%point=centre
            
            end if ! this cell is in the mesh
          
          end do ! ix
        end do ! iy
      end do ! iz

    end if ! number of mesh cells .gt.0
    
  end do ! next volume number
  
  DEALLOCATE( volume_mesh )

  CALL write_line('FINISHED: build_volume_mesh',0,output_to_screen_flag)
    
  RETURN
  
9000 CALL write_line('Error in build_volume_mesh',0,.TRUE.)
     CALL write_line_integer('Volume number',volume_number,0,.TRUE.)
     CALL write_line_integer('Surface to fill not found. Surface number',surface,0,.TRUE.)
     STOP
 
  
END SUBROUTINE build_volume_mesh
